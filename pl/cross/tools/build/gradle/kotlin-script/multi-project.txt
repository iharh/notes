import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import io.spring.gradle.dependencymanagement.dsl.DependencyManagementExtension

plugins {
    base
    kotlin("jvm") version "1.3.11" apply false
    id("org.jetbrains.kotlin.plugin.spring") version "1.3.11" apply false
    id("org.springframework.boot") version "2.1.1.RELEASE" apply false
}

allprojects {
    group = "de.mrclrchtr.education"
    version = "1.0-SNAPSHOT"

    repositories {
        jcenter()
    }
}

subprojects {
    apply(plugin = "io.spring.dependency-management")
    apply(plugin = "org.springframework.boot")

    the<DependencyManagementExtension>().apply {
        imports {
            mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        }
    }

    tasks.withType<KotlinCompile>().configureEach {
        println("Configuring $name in project ${project.name}...")
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = listOf("-Xjsr305=strict")
        }
    }

    // or
    afterEvaluate {
        tasks.withType<JavaCompile>() {
            options.isDeprecation = true
        }
    }

    tasks.withType<Test> {
        useJUnitPlatform()
    }
}
